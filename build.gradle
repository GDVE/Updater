plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'edu.sc.seis.launch4j' version '2.4.6'
    id 'application'
}

repositories {
    mavenCentral()
}

archivesBaseName = 'SimpleMinecraft'
group = 'ru.simplemc'
version = '1.0'
mainClassName = 'ru.simplemc.updater.Updater'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    compile group: 'org.rauschig', name: 'jarchivelib', version: '1.0.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.2'
    implementation group: 'com.github.oshi', name: 'oshi-core', version: '5.7.1'
    compileOnly 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Jar) {
    version = null
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

shadowJar {
    classifier = null
}

launch4j {
    version = '1.0.0.0'
    textVersion = project.version
    mainClassName = mainClassName
    icon = "${projectDir}/icon.ico"
    jar = shadowJar.archivePath
    errTitle = 'SimpleMinecraft.Ru'
    supportUrl = 'https://f.simpleminecraft.ru/'
    language = 'RUSSIAN'
    // Don't use 'Installer' and 'Updater' keywords here - exe will be created with required admin rights =|
    copyright = 'Created by Goodvise'
    fileDescription = 'SMC Starter program'
    productName = 'SMC Starter'
    internalName = 'SMC Starter'
    outfile = archivesBaseName + '.exe'
}

// Task dependency
tasks.createExe.dependsOn tasks.shadowJar
tasks.build.dependsOn tasks.shadowJar, tasks.createExe